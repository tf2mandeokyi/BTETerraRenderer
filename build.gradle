buildscript {
    repositories {
        maven { url = 'https://maven.minecraftforge.net/' }
        jcenter()
        mavenCentral()
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:3.+'
		classpath 'com.github.jengelman.gradle.plugins:shadow:4.0.0'
    }
}

apply plugin: 'net.minecraftforge.gradle'
apply plugin: 'com.github.johnrengelman.shadow'
apply plugin: 'eclipse'
apply plugin: 'maven-publish'

version = project.modVersion
group = project.modGroup
archivesBaseName = project.modId

sourceCompatibility = targetCompatibility = '1.8'
compileJava.options.encoding = 'UTF-8'

repositories {
    mavenCentral()
    maven { // cubicchunks, cubicworldgen, terraplusplus
        name = "JitPack"
        url = "https://jitpack.io/"
    }
    maven { // leveldb
        name = "DaPorkchop_"
        url = "https://maven.daporkchop.net/"
    }
    maven { // leveldb
        name = "OpenCollab Snapshots"
        url = "https://repo.opencollab.dev/snapshot/"
    }
    maven { // jankson
        name = "elytradev"
        url = "https://repo.elytradev.com/"
    }
}

minecraft {
    mappings channel: 'snapshot', version: project.mcp_mappings

    runs {
        client {
            workingDirectory project.file('run/client')
            property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'
            property 'forge.logging.console.level', 'debug'
        }
        server {
            workingDirectory project.file('run/server')
            property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'
            property 'forge.logging.console.level', 'debug'
        }
    }
}

configurations {
    shade
    compile.extendsFrom shade
}

dependencies {

    minecraft 'net.minecraftforge:forge:1.12.2-14.23.5.2859'

    compile ('com.github.OpenCubicChunks.CubicChunks:cubicchunks:f71aafb9854466ac5ffa0ccd2796921684f1d8b1') {
        transitive = false
    }
    compile ('com.github.OpenCubicChunks:CubicWorldGen:27de56d2f792513873584b2f8fd9f3082fb259ec') {
        transitive = false
    }
    compile 'com.github.BuildTheEarth:terraplusplus:f12de26053'

    shade 'org.yaml:snakeyaml:1.33'
    shade 'org.osgeo:proj4j:0.1.0'

    testCompile 'junit:junit:4.13.2'
    testCompile 'com.github.BuildTheEarth:terraplusplus:f12de26053'

    compile 'org.projectlombok:lombok:1.18.26'
    annotationProcessor 'org.projectlombok:lombok:1.18.26'
}

processResources {
    inputs.property "version", project.version
    inputs.property "mcversion", "1.12.2"

    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'
        expand 'version': project.version, 'mcversion': "1.12.2"
    }
    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}

shadowJar {
	classifier = null
	configurations = [project.configurations.shade]

	exclude 'module-info.class'
}
build.dependsOn shadowJar

reobf {
	shadowJar {}
}

task relocateShadowJar(type: com.github.jengelman.gradle.plugins.shadow.tasks.ConfigureShadowRelocation) {
	target = tasks.shadowJar
	prefix = "${project.modGroup}.${project.modId}.dep"
}
shadowJar.dependsOn relocateShadowJar